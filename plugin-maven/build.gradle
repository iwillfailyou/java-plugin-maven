plugins {
    id 'java-library'
    id 'com.jfrog.bintray' version '1.8.4'
}

sourceCompatibility = 1.8
group = ext.group

dependencies {
    implementation 'com.iwillfailyou:java-plugin:0.0.4'
    implementation 'com.iwillfailyou:java-inspections:0.7.0'
    implementation 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.6.0'
    implementation 'org.apache.maven:maven-plugin-api:3.6.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

if (project.rootProject.file('local.properties').exists()) {
    ext {
        bintrayName = 'plugin-maven'
        artifact = 'plugin-maven'
        libraryName = 'plugin-maven'

        libraryDescription = 'Iwillfailyou - is the plugin for failing not elegant projects'

        bintrayRepo = 'java'
        publishedGroupId = 'com.iwillfailyou'

        siteUrl = 'https://github.com/iwillfailyou/java-plugin-maven'
        gitUrl = 'https://github.com/iwillfailyou/java-plugin-maven.git'

        libraryVersion = '0.4.1'

        developerId = 'nikialeksey'
        developerName = 'Alexey Nikitin'
        developerEmail = 'nikialeksey@gmail.com'
        licenseName = 'MIT License'
        licenseUrl = 'https://github.com/iwillfailyou/java-plugin-maven/blob/master/LICENSE'
        allLicenses = ['MIT']
    }
    Properties properties = new Properties()
    properties.load(
            project.rootProject.file('local.properties').newDataInputStream()
    )

    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    publishing {
        publications {
            plugin_maven(MavenPublication) {
                artifact(javadocJar)
                artifact(sourcesJar)
                artifact("$buildDir/libs/plugin-maven-${libraryVersion}.jar")

                groupId publishedGroupId
                artifactId 'iwillfailyou-maven-plugin'
                version = libraryVersion

                pom {
                    name = libraryName
                    description = libraryDescription
                    packaging = 'maven-plugin'

                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'com.iwillfailyou')
                    dependencyNode.appendNode('artifactId', 'java-plugin')
                    dependencyNode.appendNode('version', '0.0.4')

                    dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'com.iwillfailyou')
                    dependencyNode.appendNode('artifactId', 'java-inspections')
                    dependencyNode.appendNode('version', '0.7.0')
                }
            }
        }
    }
    version = libraryVersion
    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        publications = ['plugin_maven']

        pkg {
            userOrg = 'iwillfailyou'
            repo = bintrayRepo
            name = bintrayName
            desc = libraryDescription
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = allLicenses
            publish = true
            publicDownloadNumbers = true
            version {
                desc = libraryDescription
                gpg {
                    sign = true
                    passphrase = properties.getProperty("bintray.gpg.password")
                }
                mavenCentralSync {
                    sync = true
                    user = properties.getProperty("bintray.sonatype.user")
                    password = properties.getProperty("bintray.sonatype.pass")
                }
            }
        }
    }
}
